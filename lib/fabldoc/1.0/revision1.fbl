/*
compile("file://../lib/fabldoc/1.0/revision1.fb","file://../lib/fabldoc/1.0/revision1.fbl");
*/


install('discovery1');
discovery1:discovery();
installDependencies('fabldoc');


startPersisting();
setHomeAndTopic('fabldoc');


var docRoot = "file://../lib/";
var docLinkRoot = "file:///C:/fabl3/lib/";
var manualLink = "file:///C:/fabl3/manual-2.0/";


var theFunctions = new(SeqOf(Function));
var theMacros = new(SeqOf(Function));
var theGlobals = new(SeqOf(id)); 
var theClasses = new(SeqOf(Class));

var globalEntries = new(ob);// binds the manual entries to the id

//stdNamespaces();

allocate('fabldoc:documentor',FunctionalProperty);
allocate('fabldoc:manualEntry',FunctionalProperty); // entry in the manual for the fellow


//var uu = mkResource();
//uu.home:docGenerator = mkResource();

// a documentor takes the thing being documented as its first argument
// and a style description as the second


fabldoc:documentor.rdfs:range = Function(void,string,ob,ob);


class('fabldoc:ManualEntry');
restrict fabldoc:page {cardinality 1;allValuesFrom id}
restrict fabldoc:anchor {cardinality 1;allValuesFrom id}
endClass();


// until the old variants are removed from manualsrc
var cPageId = 'none';

fabldoc:ManualEntry function mkManualEntry(id pg,a)
{
  var fabldoc:ManualEntry rs;
  rs  = new(fabldoc:ManualEntry);
  rs . page = cPageId;
//  rs . page = pg;
  rs . anchor = a;
  return rs;
}


fabldoc:ManualEntry function mkManualEntry(id a)
{
  return mkManualEntry(cPageId,a);
}


fabldoc:manualEntry.rdfs:range = fabldoc:ManualEntry;

void function setDocumentor(ob x,id fnm)
{
   var Function(void,string,ob,ob) fn;
   fn = getVariant(home,fnm,sequence(string,ob,ob))~Function(void,string,ob,ob);
   if (nul(fn)) error("Missing doc generator",fnm);
   x . home:documentor = fn;
}





void function documentation(string rs,ob x,ob st)
{
   var Function(void,string,ob,ob) dc;   
   dc = x . home:documentor;
   if (nul(dc)) rs * '<undocumented>';
   dc(rs,x,st);
}


// for fellows beneath lib
string function stdDocFile(string root,ob ns)
{
   var  SeqOf(ob) dfbs,ob dfb,id dnm,int ln,j,string fnm,rs,boolean libfnd,
       SeqOf(id) pth,id cp;
 //  ns = namespace(nm);
   dfbs = (ns..rdfs:isDefinedBy)~SeqOf(ob);
   if (nnul(dfbs) && (length(dfbs) == 1)) 
      {
	  dfb = dfbs[0];
	  dnm = name(dfb);
	  if (endsIn(dnm,".fb"))
	     {
		 ln = length(dnm);
		 fnm = select(dnm,0,ln-4);
		 pth = uriPath(dfb);
		 ln = length(pth);
		 libfnd = false;
		 rs = copy(root);
		 for (j = 0;j<ln-1;j++)
		    {
			cp = pth[j];
			if (libfnd) {rs * cp; rs * "/";}
			if (cp == 'lib') libfnd = true;
			}
	     rs * fnm;
		 rs * ".fabldoc.html";
		 return rs;
		 }
	   }
}



void function functionDescription(string bf,Function fn,SeqOf(id) anms,ob style)
{
   var Class tp,rtp,id nm,SeqOf(Class) itps,int ln,i,fabldoc:ManualEntry me,id a;
   me = fn.fabldoc:manualEntry;
   a = me.fabldoc:anchor;
   tp = fn.Function:type;
   nm = fn.Function:name;
   itps = inputTypes(tp);
   rtp = resultType(tp);
   writeTo(bf,'<a name="',a,'">',lf);
   writeTo(bf,'<p class="fablfunction">');
   writeTo(bf,rtp,' ',nm,'(');
   ln = length(itps);
   for (i=0;i<ln;i++)
       {
	   writeTo(bf,itps[i],' ',anms[i]);
	   if (i < (ln-1)) bf * ',';
	   }
   writeTo(bf,')',lf,'</p></a>',lf);
  push(theFunctions,fn);
}


string function addManualEntry(Class cl,id anchor)
{
  cl.manualEntry = mkManualEntry(anchor);
  push(theClasses,cl);
  return "<a name=\"{anchor}\"/>";
}

// often a page is devoted to a class


void function addManualEntry(Class cl)
{
  cl.manualEntry = mkManualEntry(nil~id);
  push(theClasses,cl);
}


string function globalEntry(id nm,id anchor)
{
  set(globalEntries,regarding(nm),mkManualEntry(anchor));
  push(theGlobals,nm);
  return "<a name=\"{anchor}\"><b>{nm}</b></a>\n";
}


string function globalEntry(id nm)
{
   return globalEntry(nm,nm);
}



var Function(void,string,Class) classLink;

void function classLink(string bf,Class cl)
{
   var fabldoc:ManualEntry me,id a,pg,nm,ob pr,string qnm,dcf,id nm,clk;
   clk = collectionKind(cl);
   if (nnul(clk))
      {
	  bf * clk;
	  bf * "(";
	  classLink(bf,collectionSubtype(cl));
	  bf * ")";
	  return;
	  }

   me = cl.fabldoc:manualEntry;
/*
   cls = collectionSubtype(cl);
   if (nnul(cls)) {bf * cl; return;}
   itps = inputTypes(cl);
   if (nnul(itps)) {bf * itps; return;}
 */  
   nm = name(cl);
   qnm = qualifiedName(cl);
   if (nul(nm)) {bf * cl; return;}
   if (nul(me)) 
      {
	  pr = parent(cl);
	  dcf = stdDocFile(docLinkRoot,pr);
	  if (nnul(dcf))
         bf * "<a href = \"{dcf}#class_{nm}\">{qnm}</a>";
	  else 
	    bf * qnm;
	  return;
	  }
   a = me.fabldoc:anchor;
   pg = me.page;
   if (nnul(a))
      bf * "<a href = \"{manualLink}{pg}.html#{a}\">{qnm}</a>";
   else
      bf * "<a href = \"{manualLink}{pg}.html\">{qnm}</a>";
}


void function functionText(string s,Function fn,string lnk)
{
  var id nm,Sort tp,SeqOfType itps,Sort rtp,int ln,lnm1,i;
  nm = fn .name;
  tp = fn . type;
  rtp = resultType(tp);
  itps = inputTypes(tp);
  writeTo(s,"<a href=\"{lnk}\">{nm}</a>",'(');
  ln =length(itps);
  lnm1 = ln-1;
  for (i = 0;i<ln;i++) 
      {
	  classLink(s,itps[i]);
	  if (i < lnm1) s * ',';
	  }
  writeTo(s,') => ',rtp);
}


void function functionLink(string bf,Function fn)
{
   var fabldoc:ManualEntry me,id a,pg;
   me = fn.fabldoc:manualEntry;
   a = me.fabldoc:anchor;
   pg = me.page;
   functionText(bf,fn,"{pg}.html#{a}");
}



void function globalLink(string bf,id gl)
{
   var fabldoc:ManualEntry me,id a,pg,Class tp;
   me = fget(globalEntries,regarding(gl))~fabldoc:ManualEntry;
   a = me.fabldoc:anchor;
   pg = me.page;
   tp = type(selectBinding(fimp,gl));
   bf * "<a href = \"{pg}.html#{a}\">{gl}</a> of type ";
	classLink(bf,tp);
	bf * "</p>\n";
}









void function macroDescription(string bf,Function fn,string txt,ob style)
{
   var Class tp,rtp,id nm,SeqOf(Class) itps,int ln,i,fabldoc:ManualEntry me,id a;
   me = fn.fabldoc:manualEntry;
   a = me.fabldoc:anchor;
   tp = fn.Function:type;
   nm = fn.Function:name;
   writeTo(bf,'<a name="',a,'">',lf);
   writeTo(bf,'<p class="fablfunction">');
   writeTo(bf,txt);
   writeTo(bf,lf,'</p></a>',lf);
  push(theMacros,fn);
}

void function genLink(string bf,Function fn)
{
   var Class tp,rtp,id nm,SeqOf(Class) itps,int ln,i,fabldoc:ManualEntry me,id a,pg;
   me = fn.fabldoc:manualEntry;
   a = me.fabldoc:anchor;
   pg = me.fabldoc:page;
   tp = fn.Function:type;
   nm = fn.Function:name;
   itps = inputTypes(tp);
   rtp = resultType(tp);
   writeTo(bf,'<a href="',pg,'.html#',a,'">');
   writeTo(bf,nm,'[');
   ln = length(itps);
   for (i=0;i<ln;i++)
       {
	   writeTo(bf,itps[i]);
	   if (i < (ln-1)) bf * ',';
	   }
   writeTo(bf,']</a>');
 }   


// ok, now for documenting libs

void function docRestrictions(string bf,Property prp,SeqOf(Restriction) rs)
{
    var int ln,i,Restriction r,int crd,Class avf;
    bf * "restrict ";
	qualifiedName(bf,prp);
	bf * " \{";
	ln = length(rs);
	for (i=0;i<ln;i++)
	    {
		r = rs[i];
		if (count(r,owl:cardinality) > 0)
		   {
		   crd = r.owl:cardinality;
		   bf * "cardinality {crd}";
		   }
	    else
		{
		if (count(r,owl:maxCardinality) > 0)
		   {
		   crd = r.owl:maxCardinality;
		   bf * "maxCardinality {crd}";
		   }
	    else
		{
		avf = r.owl:allValuesFrom~Class;
		if (nnul(avf)) 
		   {
		   bf * "allValuesFrom ";
//		   bf * avf;
		   classLink(bf,avf);
		   }
		else
		bf * "unprintable restriction"	
		}
		}    		   
		if (i < (ln-1))   bf * ";";
		}
	bf * "\}";
}


void function emitComment(string bf,cm)
{
    if (nnul(cm)) bf * "<p class=\"fablcomment\">{cm}</p>\n";
}


SeqOf(Class) function nonRestrictionSuperclasses(Class cl)
{
   var SeqOf(Class) sbs,rs,int ln,i,Class ccl;
   sbs = cl..rdfs:subClassOf;
   ln = length(sbs);
   rs = new(SeqOf(Class));
   for (i=0;i<ln;i++)
       {
	   ccl = sbs[i];
	   if (not(type0(ccl) == Restriction)) push(rs,ccl);
	   }
   return rs;
}


void function docClass(string bf,Class cl)
{
   var ob rst,bv,bk,SeqOf(Binding) bns,int ln,i,Binding cb,string qnm,id nm,
       Property prp,SeqOf(Restriction) rsq,string cm,SeqOf(Class) sbs;
   qnm = qualifiedName(cl);
   nm = name(cl);
   bf * "<p style=\"classheader\"><a name=\"class_{nm}\">";
   bf * "<b>Class {qnm}</b></a></p>\n";
    emitComment(bf,cl.rdfs:comment~string);
    bf * "<p class=\"classbody\">\n";
   bf * "class('";
   qualifiedName(bf,cl);
   bf * "');<br/>\n";
   rst = cl.restrictions;
   bns = bindings(rst);
   ln = length(bns);
   for (i=0;i<ln;i++)
       {
	   cb = bns[i];
	   bk = bindingKey(cb);
	   prp = regardingValue(bk)~Property;
	   rsq = bindingValue(cb)~SeqOf(Restriction);
	   docRestrictions(bf,prp,rsq);
	   bf * "<br/>\n";
	   }
   bf * "endClass();<br/>\n</p>";
   sbs = nonRestrictionSuperclasses(cl);
   ln = length(sbs);
   if (ln>0) 
      {
	  bf * "<p>Subclass of: ";
	  for (i=0;i<ln;i++)
	      {
		  classLink(bf,sbs[i]);
		  if (i < ln-1) bf * ", ";
		  }
	 }
	  bf * "</p>\n";
}

var namesBuf = new(SeqOf(id));


SeqOf(Class) function collectClasses(ob ns)
{
   var SeqOf(Binding) bns,SeqOf(int) prm,int ln,i,Binding cb,ob bk,bv,SeqOf(Class) rs;
   rs = new(SeqOf(Class));
   bns = bindings(ns);
   ln = length(bns);
   reset(namesBuf);
   for (i=0;i<ln;i++)
       {
	   cb = bns[i];
	   bk = bindingKey(cb);
	   if (isId(bk)) 
	      {
		  bv = bindingValue(cb);
		  if (hasType(bv,Sort)) 
		     {
			 push(rs,bv~Class);
			 push(namesBuf,bk~id);
			 }
		  }
	   }
    prm = sort:alphabetize(namesBuf);
	return sort:applyPermutation(prm,rs~SeqOf(ob))~SeqOf(Class);

}



void function docProperty(string bf,Property prp)
{
   var Class rng,id nm,string qnm;
   nm = name(prp);
   qnm = qualifiedName(prp);
   bf * "<p style=\"propertyheader\"><a name=\"property_{nm}\"><b>Property {qnm}</b></p>";
    emitComment(bf,prp.rdfs:comment~string);
   rng = prp.rdfs:range~Class;
   if (nnul(rng)) 
      {
	  bf * "<p>Range: ";
	  classLink(bf,rng);
	  bf * "</p>\n";
	  }
    bf * "<p class=\"separator\"></p>\n";

}


SeqOf(Property) function collectProperties(ob ns)
{
   var SeqOf(Binding) bns,int ln,i,SeqOf(int) prm,Binding cb,ob bk,bv,SeqOf(Property) rs;
   rs = new(SeqOf(Property));
   bns = bindings(ns);
   ln = length(bns);
   reset(namesBuf);
  for (i=0;i<ln;i++)
       {
	   cb = bns[i];
	   bk = bindingKey(cb);
	   if (isId(bk)) 
	      {
		  bv = bindingValue(cb);
		  if (hasType(bv,Property)) 
		     {
			 push(rs,bv~Property);
			 push(namesBuf,bk~id);
			 }
		  }
	   }
    prm = sort:alphabetize(namesBuf);
	return sort:applyPermutation(prm,rs~SeqOf(ob))~SeqOf(Property);
}



void function docFunction(string bf,Function fn)
{
   var Class tp,rtp,id nm,SeqOf(Class) itps,int ln,i,fabldoc:ManualEntry me,id a;
   tp = fn.Function:type;
   nm = fn.Function:name;
   itps = inputTypes(tp);
   rtp = resultType(tp);
   writeTo(bf,'<a name="function_',nm,'"/>',lf);
   writeTo(bf,'<p class="fablfunction">');
   writeTo(bf,nm,'(');
   ln = length(itps);
   for (i=0;i<ln;i++)
       {
	   classLink(bf,itps[i]);
	   if (i < (ln-1)) bf * ',';
	   }
   writeTo(bf,') => ');
   classLink(bf,rtp);
   writeTo(bf,'</p>',lf);
//  push(theFunctions,fn);
}




SeqOf(Function) function collectFunctions(ob ns)
{
   var ob fv,SeqOf(Binding) bns,SeqOf(int) prm,int ln,lnb,i,j,Binding cb,
      SeqOf(Function) bv,rs,id nm;
   rs = new(SeqOf(Function));
   fv = fget(ns,variantsP);
   reset(namesBuf);
   if (nnul(fv))
      {
	  bns = bindings(fv);
      ln = length(bns);
      for (i=0;i<ln;i++)
          {
	      cb = bns[i];
	      bv = bindingValue(cb)~SeqOf(Function);
	      rs*bv;
		  lnb = length(bv);
		  if (lnb > 0)
		     {
			 nm = bv[0].Function:name;
			 for (j=0;j<lnb;j++) push(namesBuf,nm);
			 }
	      }
	  }
    prm = sort:alphabetize(namesBuf);
	return sort:applyPermutation(prm,rs~SeqOf(ob))~SeqOf(Function);
}



void function docGlobal(string bf,Binding b)
{
   var Class tp,id nm;
   nm = name(b);
   tp = type(b);
   bf * "<p style=\"globalheader\"><a name=\"global_{nm}\"><b>Global variable {nm}</b></p>";
 //   emitComment(bf,prp.rdfs:comment~string);
	bf * "<p>Type: ";
	classLink(bf,tp);
	bf * "</p>\n";
    bf * "<p class=\"separator\"></p>\n";

}


id function globalName(Binding b)
{
   var ob bk,rv;
   bk = bindingKey(b);
   if (type0(bk) == Regarding)
      {
	  rv = regardingValue(bk);
	  if (isId(rv)) return rv~id;
	  }
   return nil ~ id;
}

SeqOf(Binding) function collectGlobals(ob ns)
{
   var SeqOf(Binding) bns,rs,int ln,i,SeqOf(int) prm,Binding cb,id nm;
   rs = new(SeqOf(Binding));
   bns = bindings(ns);
   ln = length(bns);
   reset(namesBuf);
  for (i=0;i<ln;i++)
       {
	   cb = bns[i];
	   nm = globalName(cb);
	   if (nnul(nm)) 
	      {
		  push(rs,cb);
		  push(namesBuf,nm);
		  }
	   }
    prm = sort:alphabetize(namesBuf);
	return sort:applyPermutation(prm,rs~SeqOf(ob))~SeqOf(Binding);
}




void function docHeader0(string rs)
{
rs * "<html><head>\n<link rel=\"stylesheet\" type=\"text/css\" href=\"{docLinkRoot}style.css\"/>";
}


// for debugging styles
var borderStyle = "border-width:thin;border-style:solid;";
borderStyle = "";

var libCopyrightDiv = "<div style=\"{borderStyle}"+
  "text-align:center;position:static;width:98%;" +
  "padding-left:2em;padding-right-2em;height:3em;padding-top:1em;padding-bottom:1em;\">";

var libMainDiv = "<div style=\"{borderStyle}"+
  "position:static;width:98%;" +
  "padding-left:2em;padding-right-2em;padding-top:3em;padding-bottom:5em;\">";

void function genNamespaceDoc1(string bf,id nm)
{
   var ob ns,SeqOf(Class) cls,SeqOf(Function) fns,SeqOf(Property) prps,SeqOf(Binding) gls,
      int ln,i;
   ns = namespace(nm);
   docHeader0(bf);
   bf * "<title>{nm}</title></head>\n<body>\n";
   bf * libMainDiv;
   bf * "<center><b>Namespace {nm}</b></center>\n";
   bf * "<p>URI = {uri(ns)}</p>\n";
   emitComment(bf,ns.rdfs:comment~string);
   bf * "<p><a href=\"#classes\">Classes</a><br/>";
   bf * "<a href=\"#properties\">Properties</a><br/>";
   bf * "<a href=\"#functions\">Functions</a><br/>";
   bf * "<a href=\"#globals\">Globals</a><br/></p>";

 //  installNamespace(nm);
   cls = collectClasses(ns);
   ln = length(cls);
   bf * "<a name=\"classes\"/><p><b>----CLASSES---</b></p>\n";
   for (i = 0;i<ln;i++)
       {
	   docClass(bf,cls[i]);
	   }
   prps = collectProperties(ns);
   ln = length(prps);
   bf * "<a name=\"properties\"/><p><b>----PROPERTIES----</b></p>\n";
   for (i = 0;i<ln;i++)
       {
	   docProperty(bf,prps[i]);
	   }
   fns = collectFunctions(ns);
   ln = length(fns);
   bf * "<a name=\"functions\"/><p><b>----FUNCTIONS---</b></p>\n";
   for (i = 0;i<ln;i++)
       {
	   docFunction(bf,fns[i]);
	   }
   gls = collectGlobals(ns);
   ln = length(gls);
   bf * "<a name=\"globals\"/><p><b>----GLOBALS---</b></p>\n";
   for (i = 0;i<ln;i++)
       {
	   docGlobal(bf,gls[i]);
	   }
	       bf * libCopyrightDiv;
     bf * '<center><span style="font-size: 7pt;">©Copyright 2003, 2004 Map Bureau. All rights reserved.</span></center>';
     bf * '</div>'; 

   bf * "</body>\n</html>";
}









   

void function genNamespaceDoc(id nm)
{
   var string fln,bf;
   bf = "";

   fln = stdDocFile(docRoot,namespace(nm));
   genNamespaceDoc1(bf,nm);
   fwrite(fln,bf);
}


/*

install(namespace('geom2d'));

var cc = geom2d:Point;

var rst = cc. restrictions;

var bb = bindings(rst);

var b0 = bb[0];

var vv = bindingValue(b0);

var rsq = vv ~SeqOf(Restriction);

var rr = rsq[1];

  restrict fabldoc:page {cardinality 1;allValuesFrom id}

var bf = "";
docClass(bf,geom2d:Point);


compile("file://../lib/fabldoc/1.0/revision1.fb","file://../lib/fabldoc/1.0/revision1.v1.fbl");


install(namespace('discovery1'));
discovery1:namespaces();
discovery1:discovery();
install(namespace('geom'));
install(namespace('geom2d'));
install(namespace('fabldoc'));
install(namespace('sort'));

push(path,namespace('fabldoc'));
genNamespaceDoc('geom2d');
genNamespaceDoc('sort');


var cc = new(SeqOf(Class));
collectClasses(cc,namespace('geom2d'));

var bf = "";
docClass(bf,geom2d:Point);

*/

  